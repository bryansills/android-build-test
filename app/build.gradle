apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def appVersionName = propOrDef('VERSION_NAME', "0.0.1")
def (versionMajor, versionMinor, versionPatch) = appVersionName.tokenize(".").collect { it as Integer }
def appVersionCode = (versionMajor * 1000) + (versionMinor * 100) + versionPatch

def useReleaseKeystore = rootProject.file("release/keystore.properties").exists()
def keystoreProperties = new Properties()
if (useReleaseKeystore) {
    def keystorePropertiesFile = rootProject.file("release/keystore.properties")
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        applicationId "ninja.bryansills.bob"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode appVersionCode
        versionName appVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = [fakeKey: propOrDef('FAKE_KEY', "")]
    }

    signingConfigs {
        release {
            if (useReleaseKeystore) {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile rootProject.file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
